# $Id: PKGBUILD 207669 2014-03-12 23:48:57Z foutrelis $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer (Parabola): fauno <fauno@kiwwwi.com.ar>
# Maintainer (Parabola): Andr√© Silva <emulatorman@parabola.nu>
# Maintainer (mooOS): pdq <pdq@riseup.net>

pkgbase=gstreamer0.10-bad-libre
pkgname=('gstreamer0.10-bad-libre' 'gstreamer0.10-bad-libre-plugins')
pkgver=0.10.23
pkgrel=8
arch=('i686' 'x86_64' 'mips64el')
license=('LGPL' 'GPL')
makedepends=('pkgconfig' 'gstreamer0.10-base>=0.10.36' 'xvidcore' 'libdca' 'bzip2' 'libdc1394' 'neon' 'musicbrainz' 'faad2' 'libmms' 'libcdaudio' 'libmpcdec' 'mjpegtools' 'libdvdnav' 'libmodplug' 'jasper' 'liblrdf' 'libofa' 'soundtouch' 'libvdpau' 'schroedinger' 'libass' 'libvpx' 'gsm' 'libgme' 'rtmpdump' 'libsndfile' 'librsvg' 'wildmidi' 'opus' 'git' 'spandsp' 'celt')
url="http://gstreamer.freedesktop.org/"
options=(!emptydirs)
source=("git://anongit.freedesktop.org/gstreamer-sdk/gst-plugins-bad#commit=57569a4854a0f2d14ef19a8264a4ae9a7a1d1125"
        disable-assrender-test.patch
        disable-camerabin-test.patch)
sha256sums=('SKIP'
            'e66642affa6c0e69837d37615010e67e59ef3d672663303d46c1e2591e2ddfc6'
            '01e780ddf1f8161a6115dded9dc5bf4bdd4d09a9eee00fa423b1330e90e76c68')

prepare() {
  cd gst-plugins-bad
  sed -e 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/' -i configure.ac
  patch -Np1 -i ../disable-assrender-test.patch
  patch -Np1 -i ../disable-camerabin-test.patch
}

build() {
  cd gst-plugins-bad
  NOCONFIGURE=1 ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental --disable-gtk-doc \
    --with-package-name="GStreamer Bad Plugins (mooOS GNU/Linux-libre)" \
    --with-package-origin="https://mooos.org/"

  make
  sed -e 's/gst sys ext/gst/' -i Makefile
}

check() {
  cd gst-plugins-bad
  make check
}

package_gstreamer0.10-bad-libre() {
  pkgdesc="GStreamer Multimedia Framework Bad Plugin libraries (gst-plugins-bad), without nonfree faac support"
  depends=('gstreamer0.10-base>=0.10.34')
  provides=("gstreamer0.10-bad=$pkgver")
  conflicts=('gstreamer0.10-bad')
  replaces=('gstreamer0.10-bad')

  cd gst-plugins-bad
  make DESTDIR="${pkgdir}" install
}

package_gstreamer0.10-bad-libre-plugins() {
  pkgdesc="GStreamer Multimedia Framework Bad Plugins (gst-plugins-bad), without nonfree faac support"
  depends=("gstreamer0.10-bad-libre=${pkgver}" 'xvidcore' 'libdca' 'bzip2' 'libdc1394' 'neon' 'musicbrainz' 'faad2' 'libmms' 'libcdaudio' 'libmpcdec' 'mjpegtools' 'libdvdnav' 'libmodplug' 'jasper' 'liblrdf' 'libofa' 'libvdpau' 'soundtouch' 'libass' 'schroedinger' 'libvpx' 'gsm' 'rtmpdump' 'libgme' 'libsndfile' 'librsvg' 'wildmidi' 'opus' 'celt' 'spandsp')
  groups=('gstreamer0.10-plugins')
  install=gstreamer0.10-bad-plugins.install
  provides=("gstreamer0.10-bad-plugins=$pkgver")
  conflicts=('gstreamer0.10-bad-plugins')
  replaces=('gstreamer0.10-bad-plugins')

  cd gst-plugins-bad
  make -C gst-libs DESTDIR="${pkgdir}" install
  make -C ext DESTDIR="${pkgdir}" install
  make -C sys DESTDIR="${pkgdir}" install
  make -C gst-libs DESTDIR="${pkgdir}" uninstall
}
